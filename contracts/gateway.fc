#pragma version >=0.4.0;

#include "imports/stdlib.fc";
#include "common/errors.fc";
#include "common/state.fc";
#include "common/messages.fc";
#include "common/crypto.fc";

;; Sizes ===========================================
const size::op_code_size = 32;
const size::query_id_size = 64;
const size::signature_size = 65 * 8;
const size::hash_size = 32 * 8;
const size::call_data::max = 2048 * 8; ;; 2 Kilobytes of call data

;; OP CODES ========================================
const op::internal::donate = 100;
const op::internal::deposit = 101;
const op::internal::deposit_and_call = 102;

const op::external::withdraw = 200;
const op::external::set_deposits_enabled = 201;
const op::external::update_tss = 202;
const op::external::update_code = 203;

;; GAS FEE =========================================
;; Let's use const for now and refine this later
const deposit_gas_fee = 10000000; ;; 0.01 TON

;; PARSING =========================================

;; parses the query from the message body
;; https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#internal-message-body
(slice, (int, int)) ~parse_intent(slice message_body) impure inline {
    int op = message_body~load_uint(size::op_code_size);
    int query_id = message_body~load_uint(size::query_id_size);

    return (message_body, (op, query_id));
}

;; parses full message and returns (int flags, slice sender, slice rest_of_the_message)
;;
;; TL-B
;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool
;;   src:MsgAddress dest:MsgAddressInt
;;   value:CurrencyCollection ihr_fee:Grams fwd_fee:Grams
;;   created_lt:uint64 created_at:uint32 = CommonMsgInfoRelaxed;
(int, slice, slice) parse_flags_and_sender(cell full_message) inline {
    slice cs = full_message.begin_parse();

    int flags = cs~load_uint(4);
    slice sender = cs~load_msg_addr();

    return (flags, sender, cs);
}

;; INTERNAL MESSAGES ===============================

;; Checks that deposits are enabled
() guard_deposits() impure inline_ref {
    throw_if(error::deposits_disabled, state::deposits_enabled == 0);
}

;; Protects gas usage against huge payloads
() guard_cell_size(cell data, int max_size_bits, int throw_error) impure inline {
    int max_size_cells = (max_size_bits / 1023) + 1;

    ;; note that if count(child cells) > max_size_cells, TVM will exit with code 7;
    (_, int data_bits, _) = compute_data_size(data, max_size_cells);

    if (data_bits > max_size_bits) {
        ~strdump("cell size is too big: [got, want]");
        data_bits~dump();
        max_size_bits~dump();

        throw(throw_error);
    }
}

;; deposit TON to the gateway and specify the EVM recipient on ZetaChain
() handle_deposit(slice sender, int amount, int evm_recipient) impure inline {
    load_state();
    guard_deposits();

    int deposit_amount = amount - deposit_gas_fee;

    state::total_locked += deposit_amount;
    state::fees += deposit_gas_fee;

    mutate_state();

    ;; Logs `$sender deposited $deposit_amount TON to $evm_recipient`
    cell log = begin_cell()
        .store_uint(op::internal::deposit, size::op_code_size)
        .store_uint(0, size::query_id_size)
        .store_slice(sender)
        .store_coins(deposit_amount)
        .store_uint(evm_recipient, size::evm_address)
        .end_cell();

    send_log_message(log);
}

() handle_deposit_and_call(slice sender, int amount, int evm_recipient, cell call_data) impure inline {
    load_state();
    guard_deposits();
    guard_cell_size(call_data, size::call_data::max, error::invalid_call_data);

    int deposit_amount = amount - deposit_gas_fee;

    state::total_locked += deposit_amount;
    state::fees += deposit_gas_fee;

    mutate_state();

    ;; todo Should we remove the call_data from the log?
    ;; todo Removing it reduces gas usage by ~x2.5 (wow!)

    ;; todo Should we completely remove logs and simply parse `in_msg_body`?

    ;; Logs `$sender deposited $deposit_amount TON to $evm_recipient with $call_data`
    cell log = begin_cell()
        .store_uint(op::internal::deposit_and_call, size::op_code_size)
        .store_uint(0, size::query_id_size)
        .store_slice(sender)
        .store_coins(deposit_amount)
        .store_uint(evm_recipient, size::evm_address)
        .store_ref(call_data)
        .end_cell();

    send_log_message(log);
}

;; Input for all internal messages
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    (int flags, slice sender, _) = in_msg_full.parse_flags_and_sender();

    ;; Check if the message is bouncable and fail
    ;; todo bouncED or bouncABLE?
    throw_if(error::bouncable, flags & 1);

    ;; We only want to support the basechain (id=0)
    (int wc, _) = sender.parse_std_addr();
    throw_unless(error::wrong_workchain, wc == 0);

    ;; Make sure the message contains op_code and query_id
    ;;
    ;; We DON't support op==0 aka "simple transfer with comment"
    ;; to prevent assets loss in case of a mistake tx by the user.
    throw_if(error::no_intent, in_msg_body.slice_bits() < (size::op_code_size + size::query_id_size));

    ;; query_id is not used for now.
    (int op, _) = in_msg_body~parse_intent();

    ;; Just accept the incoming value
    if (op == op::internal::donate) {
        return ();
    }

    if (op == op::internal::deposit) {
        throw_if(error::insufficient_value, msg_value < deposit_gas_fee);
        throw_if(error::invalid_evm_recipient, in_msg_body.slice_bits() < size::evm_address);

        int evm_recipient = in_msg_body~load_uint(size::evm_address);

        return handle_deposit(sender, msg_value, evm_recipient);
    }

    if (op == op::internal::deposit_and_call) {
        throw_if(error::insufficient_value, msg_value < deposit_gas_fee);
        throw_if(error::invalid_evm_recipient, in_msg_body.slice_bits() < size::evm_address);

        int evm_recipient = in_msg_body~load_uint(size::evm_address);

        throw_if(error::invalid_call_data, in_msg_body.slice_refs_empty?());

        cell call_data = in_msg_body~load_ref();

        return handle_deposit_and_call(sender, msg_value, evm_recipient, call_data);
    }

    throw(error::unknown_op);
}

;; EXTERNAL MESSAGES ===============================

;; Check ECDSA (!) signature of the external message
(cell) authenticate_external_message(slice message) impure inline_ref {
    ;; 1: Parse external message
    slice signature = message~load_bits(size::signature_size);
    int hash = message~load_uint(size::hash_size);
    cell payload = message~load_ref();

    ;; 2: Check hash validity
    throw_if(error::hash_mismatch, cell_hash(payload) != hash);

    ;; 3: Check signature
    int sig_check = check_ecdsa_signature(hash, signature, state::tss_address);

    ~strdump("check_ecdsa_signature");
    sig_check~dump();

    throw_if(error::invalid_signature, sig_check != true);

    return payload;
}

;; Withdraws assets to the recipient
;;
;; handle_withdrawal (int recipient, int amount, int seqno)
() handle_withdrawal(slice message) impure inline {
    load_state();

    ;; Authenticate the message
    slice payload = authenticate_external_message(message).begin_parse();

    ;; Parse the payload
    slice recipient = payload~load_msg_addr();
    int amount = payload~load_coins();
    int seqno = payload~load_uint(size::seqno);

    (int wc, int recipient_addr) = recipient.parse_std_addr();
    throw_unless(error::wrong_workchain, wc == 0);

    throw_if(error::insufficient_value, amount == 0);
    throw_if(error::invalid_seqno, seqno != (state::seqno + 1));

    ;; Accept the message, the contract agrees to pay gas fees
    accept_message();

    ;; Sent TON and mutate the state
    int send_mode = message::mode::ordinary + message::flag::pay_fees_separately;
    send_simple_message(recipient_addr, amount, begin_cell().end_cell(), send_mode);

    state::total_locked -= amount;
    state::seqno += 1;

    mutate_state();
}

;; Enables or disables deposits
;;
;; handle_enable_deposits (int enabled, int seqno)
() handle_set_deposits_enabled(slice message) impure inline {
    load_state();

    slice payload = authenticate_external_message(message).begin_parse();

    int enabled = payload~load_uint(1);
    int seqno = payload~load_uint(size::seqno);

    throw_if(error::invalid_seqno, seqno != (state::seqno + 1));

    accept_message();

    state::deposits_enabled = enabled;
    state::seqno += 1;

    mutate_state();
}

;; Updates the TSS address. WARNING! Execute with extra caution.
;; Wrong TSS address leads to funds loss.
;;
;; handle_update_tss (slice tss_address, int32 seqno)
() handle_update_tss(slice message) impure inline {
    load_state();

    slice payload = authenticate_external_message(message).begin_parse();

    slice new_tss_address = payload~load_bits(size::evm_address);
    int seqno = payload~load_uint(size::seqno);

    throw_if(error::invalid_seqno, seqno != (state::seqno + 1));

    accept_message();

    state::tss_address = new_tss_address;
    state::seqno += 1;

    ~strdump("new_tss_address");
    new_tss_address~dump();

    mutate_state();
}

;; Updated the code of the contract

;; handle_code_update (cell new_code, int32 seqno)
() handle_update_code(slice message) impure inline {
    load_state();

    slice payload = authenticate_external_message(message).begin_parse();

    cell new_code = payload~load_ref();
    int seqno = payload~load_uint(size::seqno);

    throw_if(error::invalid_seqno, seqno != (state::seqno + 1));

    accept_message();

    ;; note that the code will be updated only after the current tx is finished
    set_code(new_code);

    mutate_state();
}

;; Input for all external messages
() recv_external(slice message) impure {
    int op = message~load_uint(size::op_code_size);

    if (op == op::external::withdraw) {
        return handle_withdrawal(message);
    }

    if (op == op::external::set_deposits_enabled) {
        return handle_set_deposits_enabled(message);
    }

    if (op == op::external::update_tss) {
        return handle_update_tss(message);
    }

    if (op == op::external::update_code) {
        return handle_update_code(message);
    }

    throw(error::unknown_op);
}

;; GETTERS ========================================

;; returns (int1 `deposits enabled`, int128 `total TON locked`, slice[20] `TSS address`)
(int, int, slice) query_state() method_id {
    load_state();

    return (state::deposits_enabled, state::total_locked, state::tss_address);
}

;; get nonce (int32)
int seqno() method_id {
    load_state();

    return state::seqno;
}
